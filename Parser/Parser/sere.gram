program  : stat* EOF ;

stat     : assign
         | expr
         ;

assign   : IDENT (":" TYPE)? "=" expr
         ;

expr     : equality
         ;

equality : comp ( ( "!=" | "==" ) comp )* ;

comp     : term ( ( ">" | ">=" | "<" | "<=" ) term )* ;

term     : factor ( ( "+" | "-" ) factor )* ;

factor   : unary ( ( "*" | "/" ) unary )* ;

unary    : ( "-" | "!" ) unary
         | primary
         ;

primary  : NUMBER
         | STRING
         | "True"
         | "False"
         | "None"
         | "(" expr ")"
         | IDENT
         ;

NUMBER   : INTEGER | FLOAT ;
INTEGER  : [0-9]+ ;
FLOAT    : [0-9]* ( '.' [0-9]+ ) ;